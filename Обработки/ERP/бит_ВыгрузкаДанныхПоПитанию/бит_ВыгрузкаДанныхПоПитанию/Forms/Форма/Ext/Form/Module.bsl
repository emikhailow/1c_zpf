
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
	Заголовок = "Параметры выполнения регл. задания """ + ОбработкаОбъект.Метаданные().Синоним + """";
	
	ОписаниеОшибки = ОбработкаОбъект.ПолучитьПараметрыВыполненияРегламентногоЗадания(Истина);
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	Иначе
		Сообщить(ОписаниеОшибки + "
		|Настройки выполнения регл. задания не восстановлены!");
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СохранениеНастроек

&НаКлиенте
Процедура СохранитьНастройкиРегламентногоЗадания(Команда)
	
	ОписаниеОшибки = СохранитьНастройкиРегламентногоЗаданияНаСервере();
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(,"Настройки выполнения регл. задания успешно сохранены.", 20);
	Иначе
		ПоказатьПредупреждение(,ОписаниеОшибки, 20);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиРегламентногоЗаданияНаСервере()
	
	ОписаниеОшибки = "";
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		
	ОбработкаРеглЗадания = ОбработкаОбъект.ПолучитьОбработкуРегламентногоЗадания(Истина, ОписаниеОшибки);
	
	Если ОбработкаРеглЗадания.Пустая() Тогда		
		ОписаниеОшибки = ОписаниеОшибки + "
		|Настройки выполнения регл. задания не сохранены!";
		Возврат ОписаниеОшибки;
	КонецЕсли;
		
	НастройкиРеглЗадания = Новый Структура;	
	Для каждого РеквизитОбработки Из ОбработкаОбъект.Метаданные().Реквизиты Цикл
		НастройкиРеглЗадания.Вставить(РеквизитОбработки.Имя, Объект[РеквизитОбработки.Имя]);
	КонецЦикла;
	Для каждого ТабличнаяЧастьОбработки Из ОбработкаОбъект.Метаданные().ТабличныеЧасти Цикл
		НастройкиРеглЗадания.Вставить(ТабличнаяЧастьОбработки.Имя, Объект[ТабличнаяЧастьОбработки.Имя].Выгрузить());
	КонецЦикла;

	ОбъектОбработкиРеглЗадания = ОбработкаРеглЗадания.ПолучитьОбъект();
	ОбъектОбработкиРеглЗадания.ХранилищеНастроек = Новый ХранилищеЗначения(НастройкиРеглЗадания);
	
	Попытка
		ОбъектОбработкиРеглЗадания.Записать();
	Исключение
		ОписаниеОшибки = "Не получилось записать элемент справочника ""Дополнительные отчеты и обработки"": " + ОбъектОбработкиРеглЗадания + "
		|Причина: " + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

#КонецОбласти

#Область ВыполнениеРегламентногоЗадания

&НаКлиенте
Процедура ВыполнитьРегламентноеЗадание(Команда)
	ВыполнитьРегламентноеЗаданиеНаКлиенте();		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРегламентноеЗаданиеНаКлиенте(ИдентификаторКоманды = "")
	
	ОписаниеОшибки = ВыполнитьРегламентноеЗаданиеНаСервере(ИдентификаторКоманды);
	
	Если ТипЗнч(ОписаниеОшибки) = Тип("СписокЗначений") Тогда
		ОписаниеОповещенияВыбораКомандыРеглЗадания = Новый ОписаниеОповещения("ВыборКомандыРеглЗаданияЗавершение", ЭтаФорма);
		ОписаниеОшибки.ПоказатьВыборЭлемента(ОписаниеОповещенияВыбораКомандыРеглЗадания, "Выберите необходимое регл. задание");
	ИначеЕсли ПустаяСтрока(ОписаниеОшибки) Тогда	
		ПоказатьПредупреждение(,"Процедура регл. задания успешно выполнена.", 20);
	Иначе
		ПоказатьПредупреждение(,ОписаниеОшибки, 20);
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Функция ВыполнитьРегламентноеЗаданиеНаСервере(ИдентификаторКоманды = "")
	
	ОписаниеОшибки = "";
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
	ОбработкаРеглЗадания = ОбработкаОбъект.ПолучитьОбработкуРегламентногоЗадания(Истина, ОписаниеОшибки);
	
	Если ОбработкаРеглЗадания.Пустая() Тогда		
		ОписаниеОшибки = ОписаниеОшибки + "
		|Процедура регл. задания не выполнена!";
		Возврат ОписаниеОшибки;
	КонецЕсли;
	
	Если ПустаяСтрока(ИдентификаторКоманды) Тогда
		
		КомандыВызоваСервера = ОбработкаРеглЗадания.Команды.НайтиСтроки(Новый Структура("ВариантЗапуска", Перечисления.СпособыВызоваДополнительныхОбработок.ВызовСерверногоМетода));
		
		Если КомандыВызоваСервера.Количество() = 0 Тогда
			ОписаниеОшибки = "В дополнительной обработке отсутствуют команды вызова сервера!";
			Возврат ОписаниеОшибки;
		ИначеЕсли КомандыВызоваСервера.Количество() = 1 Тогда	
			ИдентификаторКоманды = КомандыВызоваСервера[0].Идентификатор;
		Иначе
			ОписаниеОшибки = Новый СписокЗначений;
			Для каждого СтрокаКоманды Из КомандыВызоваСервера Цикл
				ОписаниеОшибки.Добавить(СтрокаКоманды.Идентификатор, СтрокаКоманды.Представление);
			КонецЦикла;
			Возврат ОписаниеОшибки;
		КонецЕсли;
		
	КонецЕсли;
		
	Попытка
		ПараметрыВыполнения = Новый Структура("РучнойРежимВыполнения", Истина);
		ОбработкаОбъект.ВыполнитьКоманду(ИдентификаторКоманды, ПараметрыВыполнения);
	Исключение
		ОписаниеОшибки = "Не получилось выполнить процедуру регл. задания!
		|Причина: " + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

&НаКлиенте
Процедура ВыборКомандыРеглЗаданияЗавершение(ВыбраннаяКоманда, ДополнительныеПараметры) Экспорт
	
	Если ВыбраннаяКоманда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьРегламентноеЗаданиеНаКлиенте(ВыбраннаяКоманда.Значение);

КонецПроцедуры

#КонецОбласти
